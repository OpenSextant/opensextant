/** 
 Copyright 2009-2013 The MITRE Corporation.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.


 * **************************************************************************
 *                          NOTICE
 * This software was produced for the U. S. Government under Contract No.
 * W15P7T-12-C-F600, and is subject to the Rights in Noncommercial Computer
 * Software and Noncommercial Computer Software Documentation Clause
 * 252.227-7014 (JUN 1995)
 *
 * (c) 2012 The MITRE Corporation. All Rights Reserved.
 * **************************************************************************
**/

// This phase is conditional: 
// only to run if the document is determined to be "proper" case

Imports: {
import org.mitre.opensextant.placedata.PlaceCandidate;
 import static gate.Utils.*;
}


Phase: CheckCaseofPCProper
Input: Token Lookup placecandidate Country PersonName Admin1
Options: control = all

Rule: allLowerPCProper
(
		({placecandidate.string ==~"[-a-z\\s]+"}):pc
):phrase
-->
{
	gate.AnnotationSet pcSet = bindings.get("pc");
	gate.Annotation pc = pcSet.iterator().next();
	
	gate.FeatureMap pcFeatures = pc.getFeatures();
	PlaceCandidate pcObj = (PlaceCandidate) pcFeatures.get("placeCandidate");

	pcObj.addRuleAndConfidence(ruleName(), -4.0);
}


// TODO: change this to use abbreviation info when that info is added to the placecandidate
Rule: PluralAbbrevProper
(
		({placecandidate.string ==~"[A-Z]{1,3}s",!Country}):pc
):phrase
-->
{
	gate.AnnotationSet pcSet = bindings.get("pc");
	gate.Annotation pc = pcSet.iterator().next();
	
	gate.FeatureMap pcFeatures = pc.getFeatures();
	PlaceCandidate pcObj = (PlaceCandidate) pcFeatures.get("placeCandidate");

	pcObj.addRuleAndConfidence(ruleName(), -1.0);
}

//
Rule: CreateInitials
(
		{Token.string ==~ "[A-Z]"}({Token.string == "."})?
):phrase
-->
{
	gate.AnnotationSet phraseSet = bindings.get("phrase");
	gate.Annotation phrase = phraseSet.iterator().next();
	
	gate.FeatureMap phraseFeatures = phrase.getFeatures();
	phraseFeatures.put("rule",ruleName());
	phraseFeatures.put("string",stringFor(doc,phraseSet));

	outputAS.add(phraseSet.firstNode(), phraseSet.lastNode(), "Initials",phraseFeatures);
}

Rule: DropLowerNames
(
		{PersonName.string ==~ "[-a-z\\s]+"}
):phrase
-->
{
	gate.AnnotationSet phraseSet = bindings.get("phrase");
	gate.Annotation phrase = phraseSet.iterator().next();

	outputAS.remove(phrase);
}

// TODO: change this to use abbreviation info when that info is added to the placecandidate
Rule: DropLowerCountry
(
		{Country.string ==~ "[a-z]{1,3}"}
):phrase
-->
{
	gate.AnnotationSet phraseSet = bindings.get("phrase");
	gate.Annotation phrase = phraseSet.iterator().next();

	outputAS.remove(phrase);
}

// TODO: change this to use abbreviation info when that info is added to the placecandidate
Rule: DropLowerAdmin
(
		{Admin1.string ==~ "[a-z]{1,3}"}
):phrase
-->
{
	gate.AnnotationSet phraseSet = bindings.get("phrase");
	gate.Annotation phrase = phraseSet.iterator().next();

	outputAS.remove(phrase);
}
